apply plugin: 'maven'
apply plugin: 'signing'

group = 'games.rednblack.hyperlap2d'
version = '0.0.7'
ext.artifact = 'runtime-libgdx'
ext.packaging = 'jar'

def isDevBuild
def isCiBuild
def isReleaseBuild

def sonatypeRepositoryUrl

//set build variables based on build type (release, continuous integration, development)
if(hasProperty("RELEASE")) {
    isReleaseBuild = true
    sonatypeRepositoryUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
    println "Performing release build"
} else if (hasProperty("SNAPSHOT")) {
    isCiBuild = true
    version += "-SNAPSHOT"
    sonatypeRepositoryUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
    println "Performing snapshot build"
} else {
    isDevBuild = true
    println "Performing local build"
}

def getRepositoryUsername = {
    return project.hasProperty('NEXUS_USERNAME') ? NEXUS_USERNAME : "$System.env.NEXUS_USERNAME"
}

def getRepositoryPassword = {
    return project.hasProperty('NEXUS_PASSWORD') ? NEXUS_PASSWORD : "$System.env.NEXUS_PASSWORD"
}

repositories {
    mavenCentral()
}

task artifactDocs(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from 'build/docs/javadoc'
}

task artifactSources(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives jar
    archives artifactDocs
    archives artifactSources
}

if(isReleaseBuild) {
    signing {
        useGpgCmd()
        sign configurations.archives
    }
} else {
    task signArchives {
        // do nothing
    }
}

uploadArchives {
    repositories {
        if (isDevBuild) {
            mavenLocal()
        }
        else {
            mavenDeployer {
                if(isReleaseBuild) {
                    beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
                }

                repository(url: sonatypeRepositoryUrl) {
                    authentication(userName: getRepositoryUsername(), password: getRepositoryPassword())
                }

                pom.version = version
                pom.packaging = packaging

                pom.project {
                    artifactId = artifact
                    name 'HyperLap2D libGDX runtime'
                    description 'HyperLap2D libGDX runtime to render exported scenes'
                    url 'https://github.com/rednblackgames/hyperlap2d-runtime-libgdx'

                    scm {
                        url 'scm:git@github.com:rednblackgames/hyperlap2d-runtime-libgdx.git'
                        connection 'scm:git@github.com:rednblackgames/hyperlap2d-runtime-libgdx.git'
                        developerConnection 'scm:git@github.com:rednblackgames/hyperlap2d-runtime-libgdx.git'
                    }

                    licenses {
                        license {
                            name 'The Apache Software License, Version 2.0'
                            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                            distribution 'repo'
                        }
                    }

                    developers {
                        developer {
                            id 'fgnm'
                            name 'Francesco Marongiu'
                        }
                    }
                }
            }
        }
    }
}